OUTER_SPRINT balth_loc "%MOD_FOLDER%/balthazar"
//////////////////////////////////////////////////////////////////////////////////////////////////
////   Balthazar item/ability resources
//////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION balthazar_resources BEGIN

   LAM load_ascension_tras
   LOAD_TRA "%ascension_tra_loc%/english/description.tra" "%ascension_tra_loc%/%LANGUAGE%/description.tra"

   LAF balthazar_flip_resistance END
   LAF balthazar_stances END
   LAF balthazar_attacks END
   LAF balthazar_resources_misc END

END

DEFINE_ACTION_FUNCTION balthazar_flip_resistance BEGIN

/////////////////////////////////////////////////////////////////////////
//// 'flip resistance' swaps Balthazar's physical and magical resistances.
/// In vAsc, it just lasts 2 rds, and then reverts - but he can use it every
/// three rounds. Given modern technology I think it's more elegant, and
/// better fits the spirit of vAsc, just to let Balth flip at will between
/// the two sorts of resistance.
/// We use sectype magic to enact the flip
////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////
// 'balth11' is the basic power. Its implementation is mostly good, but:
// (i) remove its COMBATPROTECTIONS flag - with this on, breaching will counter-intuitively
//     restore Balthazar's elemental resistances, and since those resistances are *not*
//     affected by Breach, I'm inclined to treat this as a bug. (And anyway, I need that
//     slot for SECTYPE magic.)
// (ii) set a sectype
// (iii) set duration to v.long
// (iv) remove the SCRIPTINGSTATE5 - Balth has other ways to detect if the power is active,
//      and it may confuse targetting
// (v) grant the removal power; remove the existing power
// (vi) remove the delay effects
// (vii) elemental resistance consistently set to 10
///////////////////////////////////////////////////////////////////////////////

LAF add_silent_sectype STR_VAR sectype=BalthazarFlip RET sectype_value END

COPY "%balth_loc%/icons/balth11b.bam" override
COPY "%balth_loc%/resource/balth11.spl" override
     SAY NAME1 @1012
     SAY UNIDENTIFIED_DESC @22
     LPF SPL_patch 
        INT_VAR secondary=sectype_value
                ignore_dead_wild_magic=1
                castable_when_silenced=1
                level=1
     END
     WRITE_BYTE 0x27 sectype_value
     LPF ALTER_SPELL_HEADER STR_VAR icon=balth11b END
     LPF ALTER_EFFECT INT_VAR match_duration=12 duration=1000 END
     LPF DELETE_EFFECT INT_VAR match_opcode=282 END
     LPF DELETE_EFFECT INT_VAR match_timing=4 END
     LPF ALTER_EFFECT INT_VAR parameter1=10 STR_VAR match_function="opcode_is(27 28 29 30)" END
     LPF ADD_SPELL_CFEFFECT INT_VAR opcode=172 target=1 timing=1 STR_VAR resource=balth11 END
     LPF CLONE_EFFECT INT_VAR match_opcode=172 opcode=171 STR_VAR resource=finbafl END
// BUT_ONLY

///////////////////////////////////////////////////////////
/// 'finbafl' is the new
/// power, which just removes balth11b (and grants/removes
/// the powers itself)
//////////////////////////////////////////////////////////////

COPY_EXISTING "balth11.spl" "override/finbafl.spl"
   SAY UNIDENTIFIED_DESC @23
   LPF SPL_patch INT_VAR level=1 END
   LPF DELETE_EFFECT STR_VAR match_function="opcode_is_not(9 174 215 139 171 172)" END // leave cosmetic bits only
   LPF ADD_SPELL_EFFECT INT_VAR opcode=221 target=1 timing=1 parameter1=10 parameter2=sectype_value END
   LPF ALTER_EFFECT INT_VAR match_opcode=172 STR_VAR resource=finbafl END
   LPF ALTER_EFFECT INT_VAR match_opcode=171 STR_VAR resource=balth11 END
   LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@1063) END
// BUT_ONLY


END


DEFINE_ACTION_FUNCTION balthazar_stances BEGIN

/////////////////////////////////////////////////////////////////////////
//// We're going here with evidence from various bits of vAsc and the vanilla
//// game that you're 'supposed' to have only one stance at a time - but 
//// that wasn't very implementable with the previous system. 
////
//// So now stances are indefinite (well, 1000 seconds) and remove previous
//// stances.
////
//// On EE this is simple - use 321. On vanilla, we use sectype removal - but
//// since Solar Stance and Lunar Stance need to be labelled as Specific, and
//// Shadow as Illusory, this means some small danger of removing other things.
////////////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////////
//// Solar stance:
////
//// - sunfire effect with knockback
//// - 127% immunity to fire, not modifiable by other effects
//// - attacks do an extra 1d10 fire damage, transferred to Balthazar
////
////  - the actual (vanilla, unmodified) effect is weirdly constructed,
////   with two headers - the first looks better built, but includes
////   a sleep effect that the caster needs immunity to .
////   We'll strip the second header away.
//// -  We also need to give immunity to the 'fire resistance' opcodes,
////   and do the fire resistance more carefully, and mess with durations
//// - we need to remove and reapply the various stances
//// - it needs to target self
//// - move the resistance powers into the header ('effect' powers don't get removed by 221)
//// - Balth needs a second of crushing and sleep resistance (on non-EE) or immunity to his own effect, after
//// - the boosting spell has been cast (on EE) - for this reason, order is crucial
/////////////////////////////////////////////////////////////////////////

    COPY_EXISTING "balth01.spl" override
        SAY UNIDENTIFIED_DESC @24
     LPF SPL_patch INT_VAR level=1 END
        LPF DELETE_SPELL_HEADER INT_VAR header_type="-1" min_level=2 END
        LPF ALTER_SPELL_HEADER INT_VAR target=5 END
        PATCH_IF enhanced_edition BEGIN
            LPF ADD_SPELL_EFFECT INT_VAR insert_point=2 opcode=318 target=1 duration=1 STR_VAR resource=balth01 END
            LPF DELETE_EFFECT INT_VAR match_opcode=101 match_parameter2=235 END
        END ELSE BEGIN
            LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=87 target=1 duration=1000 parameter1=100 parameter2=0 END
            LPF ALTER_EFFECT INT_VAR match_opcode=101 match_parameter2=235 duration=2 END
            LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=235 parameter2=39 END
        END
        PATCH_FOR_EACH match_opcode IN 30 84 BEGIN
           LPF DELETE_EFFECT INT_VAR match_opcode END
            LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=match_opcode target=1 duration=1000 parameter1=127 parameter2=0 END
           LPF CLONE_EFFECT INT_VAR match_opcode opcode=101 parameter1=0 parameter2=match_opcode STR_VAR insert=below END
        END
        LPF stance_helper STR_VAR this_stance="%SOURCE_RES%" END
    BUT_ONLY

    // balth01a: helper spell that applies the melee hit effect
    // needs the sectype

    COPY_EXISTING balth01a.spl override
      LPF SPL_patch INT_VAR secondary=2 END
      LPF ALTER_EFFECT INT_VAR match_opcode=248 duration=1000 END

    // the actual effect, balth01b.eff, doesn't exist
    
    COPY "%balth_loc%/resource/balth01b.eff" override
    
    // the effect, on EE, needs to use 'drain hp'

    ACTION_IF enhanced_edition BEGIN
         COPY_EXISTING balth01b.spl override
            LPF DELETE_EFFECT INT_VAR match_opcode=17 END
            LPF ALTER_EFFECT INT_VAR match_opcode=12 drain_hp_to_caster=1 END
    END

/////////////////////////////////////////////////////////////////////////
//// Lunar stance:
//// - immunity to energy drain, timestop, imprisonment; +5 to saves vs death; +2 luck
//// - also immunity to death effect, disintegrate (relics of what was originally Avoid Death, I think) but
////   that doesn't matter
//// - loses hp boost from original version
//// - add some cosmetic effects (2-second pause target, sparkle)
//// - add a DS state, SCRIPTINGSTATE1=5 (used by balth and others to detect it)
////
//// - In vAsc, immediately after entering Lunar Stance Balth triggers a modified Glitterdust
////   and single-target Malison effect, via ReallyForceSpell. This seems to be a legacy of the original
////   Balthazar script, but it's a bit awkward both visually and as a matter of coding.
////   I meddle: the Glitterdust effect becomes an automatic, center-self effect of adopting Lunar
////   Stance, and the stance grants Lunar Malison (=balth07). Since this mildly hurts Balth's action 
////   economy, I restore the Slow ability that balth07 has in vanilla.
////
/////////////////////////////////////////////////////////////////////////

COPY_EXISTING balth02.spl override
     SAY UNIDENTIFIED_DESC @25
     LPF SPL_patch INT_VAR level=1 END
     LPF ALTER_EFFECT INT_VAR match_duration=30 duration=1000 END
     LPF ALTER_EFFECT INT_VAR target=1 END
     LPF DELETE_EFFECT INT_VAR match_opcode=18 END  // max hp
     LPF ADD_SPELL_EFFECT INT_VAR opcode=22 target=1 parameter1=2 duration=1000 resist_dispel=2 END // luck bonus
     PATCH_FOR_EACH parameter2 IN 1 2 BEGIN
        LPF ADD_SPELL_EFFECT INT_VAR opcode=41 target=1 parameter1=10 parameter2 duration=3 resist_dispel=2 END  // sparkles
     END
     LPF ADD_SPELL_EFFECT INT_VAR opcode=165 target=1 duration=2 resist_dispel=2 END // pause target
     parameter2=(IDS_OF_SYMBOL (stats SCRIPTINGSTATE1)) - 156
     LPF ADD_SPELL_EFFECT INT_VAR opcode=282 target=1 duration=1000 resist_dispel=2 parameter1=5 parameter2 END
     LPF stance_helper STR_VAR this_stance="%SOURCE_RES%" END
     LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=2 timing=1 parameter2=1 STR_VAR resource=balth12 END
BUT_ONLY

// Lunar Malison:
// this is already in the vanilla game, but we tweak it:
// - rename
// - no LOS needed
// - change projectile
// - redo Slow
// - mark up for DS
// - remove spell when used (since Balth's script casts with ForceSpell)
// (vAsc version suppresses some animations, but I think that's an implementation error)
// - needs to give immunity to itself, Mel's version, and vanilla GM, or similar; ought to edit vanilla GM too
// (the version in Fixpack and in EE protects against SPWI412, but not against itself)

COPY_EXISTING balth07.spl override
  SAY NAME1 @1068
  SAY UNIDENTIFIED_DESC @32
  LPF SPL_patch
     INT_VAR no_los_required=1
             spell_type=4
             level=1
  END
  LPF ALTER_SPELL_HEADER INT_VAR projectile=139 location=4 END
  LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=172 timing=1 target=1 STR_VAR resource=balth07 END
  LPF CLONE_EFFECT STR_VAR match_resource="%WIZARD_GREATER_MALISON%" resource=balth07 insert=last END
  LPF CLONE_EFFECT STR_VAR match_resource="%WIZARD_GREATER_MALISON%" resource=melspl07 END
  LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=282 parameter1=1 parameter2=(IDS_OF_SYMBOL (stats WIZARD_GREATER_MALISON)) - 156 END
  LPF ALTER_EFFECT INT_VAR match_opcode=40 opcode=177 STR_VAR resource=finslow END
BUT_ONLY

COPY_EXISTING "%WIZARD_GREATER_MALISON%.spl" override
  LPF CLONE_EFFECT STR_VAR match_resource="%WIZARD_GREATER_MALISON%" resource=balth07 END
BUT_ONLY


// BALTH12: new power, but it's a largely-cosmetic variant on Glitterdust
// substantive changes: range=30, speed=0, ability type=melee, power=4
// cosmetic changes: projectile changes to 169 (blue); the 215 visual is stripped, and replaced
// with some sparkles
// DW: suppress name

COPY_EXISTING "spwi224.spl" "override/balth12.spl"
    WRITE_LONG 0x8 "-1"
    LPF ALTER_SPELL_HEADER INT_VAR new_header_type=1 range=30 speed=0 projectile=169 END
    LPF DELETE_EFFECT INT_VAR match_opcode=215 END
    LPF ALTER_EFFECT INT_VAR power=4 END
    LPF ADD_SPELL_EFFECT INT_VAR opcode=41 target=2 power=4 parameter1=10 parameter2=1 duration=3 resist_dispel=3 END
    LPF CLONE_EFFECT INT_VAR match_opcode=41 parameter2=3 END
// BUT_ONLY

/////////////////////////////////////////////////////////////////////////
//// Shadow Stance:
//// - improved invisibility, with an additional -6 AC (pure invisibility lasts only 15 sec)
//// - duration needs patching
/////////////////////////////////////////////////////////////////////////

COPY_EXISTING balth10.spl override
     LPF SPL_patch INT_VAR level=1 END
     SAY UNIDENTIFIED_DESC @26
     LPF ALTER_EFFECT INT_VAR match_duration=30 duration=1000 END
     LPF stance_helper STR_VAR this_stance="%SOURCE_RES%" END
BUT_ONLY

// end of Stance block

END


//////////////////////////////////////////////////////
/// Helper function that grants the stances, strips
/// their effects, etc
///////////////////////////////////////////////////////


DEFINE_PATCH_FUNCTION stance_helper
   STR_VAR this_stance=""
BEGIN
   LPF ALTER_SPELL_HEADER INT_VAR speed=1 STR_VAR icon="%this_stance%b" END
   INNER_ACTION BEGIN
       COPY "%balth_loc%/icons/%this_stance%B.bam" override
   END
   LPF SPL_patch
      INT_VAR ignore_dead_wild_magic=1
              castable_when_silenced=1
   END
   PATCH_FOR_EACH resource IN balth01 balth02 balth10 BEGIN
     LPF ADD_SPELL_CFEFFECT INT_VAR opcode=171 target=1 timing=1 STR_VAR resource END
     LPF ADD_SPELL_CFEFFECT INT_VAR opcode=172 target=1 timing=1 STR_VAR resource END
   END
   PATCH_MATCH "%this_stance%" WITH
   balth02 BEGIN
      LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=171 target=1 timing=1 STR_VAR resource=balth07 END
   END
   DEFAULT
      LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=172 target=1 timing=1 STR_VAR resource=balth07 END
   END
   LPF DELETE_EFFECT INT_VAR match_opcode=171 STR_VAR match_resource="%this_stance%" END
   PATCH_IF enhanced_edition BEGIN
       PATCH_FOR_EACH resource IN balth01 balth02 balth10 BEGIN
          PATCH_IF !"%resource%" STRING_EQUAL_CASE "%this_stance%" BEGIN
             LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=1 timing=1 STR_VAR resource END
          END
       END
       LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=1 STR_VAR resource=balth01a END
   END ELSE BEGIN
       LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=221 target=1 timing=1 parameter1=10 parameter2=2 END
       LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=221 target=1 timing=1 parameter1=10 parameter2=3 END
   END


END

////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////
/// Balthazar's attack forms
////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION balthazar_attacks BEGIN


/////////////////////////////////////////////////////////////
/// Dragon Fist
/// This is an antimagic attack, with a 75% chance of dispelling
/// each of the target's combat and specific  protections, and a
/// a 75% chance of dispelling the spell protections. (DW: the combat
/// bit is missing from vAsc, but you can tell it's supposed to be
/// there from the scripting.) It also imposes a 2-round Slow effect,
/// with no saving throw. Usable every 3 rounds.
///
/// This is a complete rewrite of the vanilla power so we use the vAsc
/// version, largely unchanged.
/// - (Minor implementation note: the probabilities aren't quite independent.
/// I don't think it matters.)
/// - incorporate the animation into the spell directly
/// - add some strings
/// - correct probability error on icon removal
/// - correct off-by-one error
/// - handle Slow more systematically
/////////////////////////////////////////////////////////////

     COPY "%balth_loc%/resource/balth03.spl" override
         SAY UNIDENTIFIED_DESC @28
         LPF SPL_patch INT_VAR level=1 END
         LPF general_balth_helper INT_VAR repeat=18 STR_VAR this_resource=balth03 END
         LPF ALTER_SPELL_HEADER INT_VAR speed=0 target=1 END
         LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=138 target=1 timing=0 parameter2=13 END
         LPF CLONE_EFFECT INT_VAR match_opcode=230 match_parameter2=2 opcode=139 parameter2=0 parameter1=RESOLVE_STR_REF (@1065) END
         LPF CLONE_EFFECT INT_VAR match_opcode=230 match_parameter2=2 parameter2=7 END
         LPF CLONE_EFFECT INT_VAR match_opcode=230 match_parameter2=1 opcode=139 parameter2=0 parameter1=RESOLVE_STR_REF (@1064) END
         LPF CLONE_EFFECT INT_VAR match_opcode=40 opcode=139 parameter2=0 parameter1=14668 END
         LPF ALTER_EFFECT INT_VAR match_opcode=240 probability2=25 END
         LPF ALTER_EFFECT INT_VAR match_probability1=75 probability1=74 END
         LPF ALTER_EFFECT INT_VAR match_opcode=174 STR_VAR resource=HIT_02B END
         LPF ALTER_EFFECT INT_VAR match_opcode=40 opcode=177 STR_VAR resource=finslow END
     BUT_ONLY

///////////////////////////////////////////////////////////////////////////
// Tiger Strike:
//
// This is largely unchanged from vanilla, and is based on Smite. For 2 rounds, Balth gets:
// - 50% chance of a critical hit (nerfed from 100% in vanilla).
// - 100% chance of knockback and 6 seconds of stunning
//
// Usable once per 4 rds
//
// vAsc changes, which we copy:
// - level=1
// - critical-hit modifier nerfed from +20 to +10
// - animation should be present for 3 secs, not 0 secs (unsure if it matters but we'll do it anyway)
///////////////////////////////////////////////////////////////////////////

COPY_EXISTING balth04.spl override
     SAY UNIDENTIFIED_DESC @29
     LPF SPL_patch INT_VAR level=1 END
     LPF ALTER_EFFECT INT_VAR match_opcode=215 duration=3 END
     LPF ALTER_EFFECT INT_VAR match_opcode=301 parameter1=10 END
     LPF general_balth_helper INT_VAR repeat=24 STR_VAR this_resource=balth04 END
BUT_ONLY

///////////////////////////////////////////////////////////////////////////
// Hand of Murder:
// -Automatic critical hits for 1 rd
// -(added by vAsc): Quivering Palm effect for the first half-round (save vs.spell or die)
// - 1/3 rounds
//
// vAsc tweaks
// - level=1
// - power=0
// - fix a (perhaps harmless?) error in the timing type for the visual effect (it's instant/permanent, should be temporary)
// - add the melee hit effects of Quivering Palm (SPCL820 and QUIVVIS)
///////////////////////////////////////////////////////////////////////////

COPY_EXISTING balth05.spl override
     LPF SPL_patch INT_VAR level=1 END
     SAY UNIDENTIFIED_DESC @30
     LPF ALTER_EFFECT INT_VAR power=0 END
     LPF ALTER_EFFECT INT_VAR timing=0 match_opcode=215 END
     PATCH_FOR_EACH resource IN spcl820 quivvis BEGIN
         LPF ADD_SPELL_EFFECT INT_VAR opcode=248 target=2 duration=3 resist_dispel=2 STR_VAR resource END
     END
     LPF general_balth_helper INT_VAR repeat=18 STR_VAR this_resource=balth05 END
BUT_ONLY

///////////////////////////////////////////////////////////////////////////
// Shadowless kick:
// wing buffet effect, with 4d8 damage
//
// This is unmodified in vAsc; all we do is bring the animation inwards, as with Dragon Fist
///////////////////////////////////////////////////////////////////////////

COPY_EXISTING balth09.spl override
    SAY UNIDENTIFIED_DESC @31
     LPF SPL_patch INT_VAR level=1 END
    LPF ALTER_SPELL_HEADER INT_VAR speed=0 target=1 END
    LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=138 target=1 timing=0 parameter2=12 END
    LPF general_balth_helper INT_VAR repeat=12 STR_VAR this_resource=balth09 END
BUT_ONLY


END

////////////////////////////////////////////////////////////
/// Miscellaneous stuff
////////////////////////////////////////////////////////////
DEFINE_ACTION_FUNCTION balthazar_resources_misc BEGIN

// Faster than the Eye: change of animation; teleport uses Instant/Permanent, not Delay/Permanent
// DW addition: graphically, casting time needs to be zero

COPY_EXISTING balth08.spl override
    SAY UNIDENTIFIED_DESC @27
     LPF SPL_patch INT_VAR level=1 END
    REPLACE_TEXTUALLY CASE_INSENSITIVE SPRMPARI SPFLESHS
    LPF ALTER_SPELL_HEADER INT_VAR speed=0 END
    LPF ALTER_EFFECT INT_VAR match_opcode=124 timing=1 END
    LPF general_balth_helper INT_VAR repeat=12 STR_VAR this_resource=balth08 END
BUT_ONLY

// A two-round single-target no-save Slow effect, used by Dragon Fist and Lunar Malison

COPY_EXISTING "%WIZARD_SLOW%.spl" "override/finslow.spl"
   LPF ALTER_SPELL_HEADER INT_VAR projectile=1 END
   LPF ALTER_EFFECT INT_VAR match_duration=60 duration=12 END
   LPF ALTER_EFFECT INT_VAR match_save_vs_spell=1 save_vs_spell=0 savebonus=0 END
   // manage the various immunities
   PATCH_IF enhanced_edition BEGIN
      LPF CLONE_EFFECT INT_VAR match_opcode=321 STR_VAR match_resource="%WIZARD_SLOW%" resource=finslow insert=first END
   END ELSE BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode=206 STR_VAR match_resource="%WIZARD_SLOW%" resource=finslow END
      LPF CLONE_EFFECT INT_VAR match_opcode=206 STR_VAR match_resource=spwm164 resource="%WIZARD_SLOW%" END
   END

ACTION_FOR_EACH spell IN SPWM164 "%WIZARD_SLOW%" SPWISH25 BEGIN
   COPY_EXISTING "%spell%.spl" override
       SET match_opcode = enhanced_edition? 321 : 206
       LPF CLONE_EFFECT INT_VAR match_opcode STR_VAR match_resource="%spell%" resource=finslow insert=first END
END

ACTION_IF FILE_EXISTS_IN_GAME mels545.spl BEGIN
   FAIL "Balth function must precede Mel function so that slow effects are built correctly"
END

// EFF file to cast the spell

CREATE EFF finslow
   WRITE_LONG 0x10 146
   WRITE_LONG 0x14 2
   WRITE_LONG 0x20 1
   WRITE_LONG 0x24 9
   WRITE_SHORT 0x2c 100
   WRITE_ASCII 0x30 finslow
   
// allow for the spell in various Free Action resources

COPY_EXISTING_REGEXP
  ".*\.spl" override
  ".*\.itm" override
     PATCH_IF INDEX_BUFFER ("%WIZARD_SLOW%")>=0 BEGIN
         LPF CLONE_EFFECT INT_VAR silent=1 STR_VAR match_function="opcode_is(321 206)" match_resource="%WIZARD_SLOW%" resource=finslow END

     END
BUT_ONLY



END
////////////////////////////////////////////////////////////
/// Helper function for most powers
////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION general_balth_helper
    INT_VAR repeat=0
    STR_VAR this_resource=""
BEGIN
    LPF SPL_patch
      INT_VAR ignore_dead_wild_magic=1
              castable_when_silenced=1
    END
    LPF ALTER_SPELL_HEADER STR_VAR icon="%this_resource%B" END
    INNER_ACTION BEGIN
      COPY "%balth_loc%/icons/%this_resource%B.bam" override
    END
    LPF ADD_SPELL_CFEFFECT INT_VAR opcode=171 target=1 timing=(repeat=0?1:4) duration=repeat STR_VAR resource="%this_resource%" END
    LPF ADD_SPELL_CFEFFECT INT_VAR opcode=172 target=1 timing=1 STR_VAR resource="%this_resource%" END
END
