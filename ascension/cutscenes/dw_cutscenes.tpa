/////////////////////////////////////////////////////////////////////////////////
//   Main function wrapper
////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION dw_cutscenes BEGIN

   LAF gromnir_cutscene END
   LAF yaga_cutscene END


END


/////////////////////////////////////////////////////////////////////////////////
//   Instead of the party standing around like lemons when Gromnir has Melissan
//   dragged off, have them arrive afterwards (really, by suppressing their
//   animations, and then removing the suppression effect via sectype magic).//
////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION gromnir_cutscene BEGIN

//////////////////////////
// Build animation-
// suppressing spell
//////////////////////////

     ADD_SECTYPE cutscene_invisibility ""
    
     LAF create_basic_spell STR_VAR spell=finhdpa END
     COPY_EXISTING finhdpa.spl override
          WRITE_BYTE 0x27 cutscene_invisibility
          LPF ADD_SPELL_EFFECT INT_VAR opcode=315 target=3 parameter2=1 duration=999999 END
          LPF ADD_SPELL_EFFECT INT_VAR opcode=287 target=3 duration=999999 END
     BUT_ONLY

     LAF create_basic_spell STR_VAR spell=finrvpa END
     COPY_EXISTING finrvpa.spl override
          LPF ADD_SPELL_EFFECT INT_VAR opcode=221 target=3 parameter1=10 parameter2=cutscene_invisibility END
     BUT_ONLY

/////////////////////////////////////////////////////////
/// Edit area file to make area trigger into info trigger
/////////////////////////////////////////////////////////

     COPY_EXISTING "ar5001.are" override
           GET_OFFSET_ARRAY reg_arr ARE_V10_REGIONS
           PHP_EACH reg_arr AS discard=>offset BEGIN
                READ_ASCII offset trig_name
                PATCH_IF "%trig_name%" STRING_EQUAL "Tran5002" BEGIN
                   WRITE_BYTE (0x61+offset) (SHORT_AT (0x61 + offset)) BOR 0b00000100
                   WRITE_SHORT (0x20+offset) 1 // type
                   WRITE_SHORT (0x84+offset) 916
                   WRITE_SHORT (0x86+offset) 340
                   WRITE_ASCII (0x7c+offset) gromnisc
                END
           END
     BUT_ONLY

//////////////////////////////
// script for info trigger
//////////////////////////////

     <<<<<<<< .../ascension-inline/gromnisc.baf
IF
       WalkedToTrigger([ANYONE])
       !Global("fin_grom_cs","GLOBAL",1) 
       !IsValidForPartyDialogue("dorn")
THEN
     RESPONSE #100
       SetGlobal("fin_grom_cs","GLOBAL",1)
       ApplySpellRES("finhdpa",Player1)
       Wait(1)
       ActionOverride(Player1,LeaveAreaLUA("AR5002","",[800.600],SSW))
       ActionOverride(Player2,LeaveAreaLUA("AR5002","",[810.600],SSW))
       ActionOverride(Player3,LeaveAreaLUA("AR5002","",[800.610],SSW))
       ActionOverride(Player4,LeaveAreaLUA("AR5002","",[800.590],SSW))
       ActionOverride(Player5,LeaveAreaLUA("AR5002","",[790.600],SSW))
       ActionOverride(Player6,LeaveAreaLUA("AR5002","",[790.590],SSW))
END

IF
       WalkedToTrigger([ANYONE])
       OR(2)
            Global("fin_grom_cs","GLOBAL",1)
            IsValidForPartyDialogue("dorn")
THEN
     RESPONSE #100
       SetGlobal("fin_grom_cs","GLOBAL",1)
       ActionOverride(Player1,LeaveAreaLUA("AR5002","",[800.600],SSW))
       ActionOverride(Player2,LeaveAreaLUA("AR5002","",[810.600],SSW))
       ActionOverride(Player3,LeaveAreaLUA("AR5002","",[800.610],SSW))
       ActionOverride(Player4,LeaveAreaLUA("AR5002","",[800.590],SSW))
       ActionOverride(Player5,LeaveAreaLUA("AR5002","",[790.600],SSW))
       ActionOverride(Player6,LeaveAreaLUA("AR5002","",[790.590],SSW))
END
     >>>>>>>>
COMPILE ".../ascension-inline/gromnisc.baf"

//////////////////////////////////
// tweak throne room cutscenes
//////////////////////////////


<<<<<<<<  .../ascension-inline/fincs225.baf
        IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		MoveViewObject("cutspy",VERY_FAST)
		Wait(2)
		ActionOverride("mel01",StartDialogueNoSet(Player1))
END
>>>>>>>>

COMPILE ".../ascension-inline/fincs225.baf"

COPY_EXISTING "cut225b.bcs" override
   DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY "Wait(4)" ~Wait(8)ApplySpellRES("finrvpa",Player1)~
   END
BUT_ONLY

//////////////////////////////////////////////////////////////////////////////////////////////////////
// tweak throne room area script so different scripts fire according to whether Dorn is in the party
//////////////////////////////////////////////////////////////////////////////////////////////////


LAF CLONE_SCRIPT_BLOCK
  STR_VAR script=ar5002
          match=~("EnteredArea","AR5002",0)~
          original_swap_out=~OR(2)\(%TAB%\|%WNL%\|%LNL%\| \)*![a-z]*("Dorn")\(%TAB%\|%WNL%\|%LNL%\| \)*Global("OHD_gromnir","AR5002",2)~
          original_swap_in=~!IsValidForPartyDialogue("Dorn")~
          original_swap_out1=cut225a
          original_swap_in1=fincs225
          swap_out=~OR(2)\(%TAB%\|%WNL%\|%LNL%\| \)*![a-z]*("Dorn")\(%TAB%\|%WNL%\|%LNL%\| \)*Global("OHD_gromnir","AR5002",2)~
          swap_in=~Global("OHD_gromnir","AR5002",2)~
  END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////
//Make sure the thugs who attack the party just before they meet Y-S all say different lines
//////////////////////////////////////////////////////////////////////////////////////////////////




DEFINE_ACTION_FUNCTION yaga_cutscene BEGIN

<<<<<<<< .../ascension-inline/area_add.baf
IF
  Global("fin_grunts","AR5203",1)
THEN
    RESPONSE #100
        DisplayStringHead("fingrun1",72708)
        ActionOverride("fingrun1",Enemy())
        Wait(1)
        DisplayStringHead("fingrun2",72709)
        ActionOverride("fingrun2",Enemy())
        Wait(1)
        DisplayStringHead("fingrun3",72710)
        ActionOverride("fingrun3",Enemy())
        SetGlobal("fin_grunts","AR5203",2)
END
>>>>>>>>

EXTEND_TOP ar5203.bcs ".../ascension-inline/area_add.baf"

<<<<<<<< .../ascension-inline/ysgrunt.baf
IF
	See([PC])
	See("yspeas")
	!StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
	Global("Shout2","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("Shout2","LOCALS",1)
		DisplayStringHead(Myself,72707) // Ha ha ha!
		AttackOneRound("yspeas")
END

IF
	See([PC])
	Global("fin_grunts","AR5203",0)
THEN
	RESPONSE #100
	   SetGlobal("fin_grunts","AR5203",1)
END

IF
	See("yspeas")
	!StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
	!Range(LastSeenBy(Myself),5)
	Global("fin_grunts","AR5203",0)
THEN
	RESPONSE #100
		MoveToObject(LastSeenBy(Myself))
END


>>>>>>>>

COMPILE ".../ascension-inline/ysgrunt.baf"

ACTION_FOR_EACH grunt_number IN 1 2 3 BEGIN
   COPY_EXISTING "ysgrunt.cre" "override/fingrun%grunt_number%.cre"
        WRITE_ASCII DEATHVAR "fingrun%grunt_number%"
END

COPY_EXISTING "ar5203.bcs" override
      DECOMPILE_AND_PATCH BEGIN
         SET grunt_number=0
         REPLACE_EVALUATE CASE_INSENSITIVE "ysgrunt" BEGIN
              grunt_number+=1
         END
         "fingrun%grunt_number%"
      END
BUT_ONLY



END