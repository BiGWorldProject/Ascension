DEFINE_ACTION_FUNCTION testing_suite BEGIN

// force a savegame

<<<<<<<< .../ascension-inline/force_save.baf
IF
  Global("DW_force_save","GLOBAL",1)
THEN                                
   RESPONSE #100
            SetGlobal("DW_force_save","GLOBAL",0)
            SaveGame(0)
END
>>>>>>>>

EXTEND_TOP baldur25.bcs ".../ascension-inline/force_save.baf"

// set the dream states all to 'good' or 'evil'; autocomplete the challenges

<<<<<<<< .../ascension-inline/dream_state.baf
IF
  Global("DW_test_evil_dreamstates","GLOBAL",1)
THEN
    RESPONSE #100
      SetGlobal("test_evil_dreamstates","GLOBAL",0)
      SetGlobal("Bhaal25Dream1","GLOBAL",-10)
      SetGlobal("Bhaal25Dream2","GLOBAL",-10)
      SetGlobal("Bhaal25Dream3","GLOBAL",-10)
      SetGlobal("Bhaal25Dream4","GLOBAL",-10)
      SetGlobal("Bhaal25Dream5","GLOBAL",-10)
      Continue()
END

IF
  Global("DW_test_good_dreamstates","GLOBAL",1)
THEN
    RESPONSE #100
      SetGlobal("test_good_dreamstates","GLOBAL",0)
      SetGlobal("Bhaal25Dream1","GLOBAL",10)
      SetGlobal("Bhaal25Dream2","GLOBAL",10)
      SetGlobal("Bhaal25Dream3","GLOBAL",10)
      SetGlobal("Bhaal25Dream4","GLOBAL",10)
      SetGlobal("Bhaal25Dream5","GLOBAL",10)
      Continue()
END

IF
  Global("DW_test_first_challenge","GLOBAL",1)
THEN
    RESPONSE #100
             SetGlobal("test_first_challenge","GLOBAL",0)
             SetGlobal("SPRITE_IS_DEADCHALPC01","GLOBAL",1)
             SetGlobal("SPRITE_IS_DEADCHTAZ01","GLOBAL",1)
             SetGlobal("SPRITE_IS_DEADCHANG01","GLOBAL",1)
             SetGlobal("SPRITE_IS_DEADCHSAM01","GLOBAL",1)
    Continue()
END

IF
  Global("DW_test_second_challenge","GLOBAL",1)
THEN
    RESPONSE #100
             SetGlobal("test_second_challenge","GLOBAL",0)
             SetGlobal("SPRITE_IS_DEADCHALSLAY","GLOBAL",1)
    Continue()
END

IF
  Global("DW_test_third_challenge","GLOBAL",1)
THEN
    RESPONSE #100
             SetGlobal("test_third_challenge","GLOBAL",0)
             SetGlobal("SPRITE_IS_DEADCHALCY02","GLOBAL",3)
    Continue()
END

IF
  Global("DW_test_fifth_challenge","GLOBAL",1)
THEN
    RESPONSE #100
             SetGlobal("test_fifth_challenge","GLOBAL",0)
             SetGlobal("SPRITE_IS_DEADCHALRV01","GLOBAL",1)
    Continue()
END
>>>>>>>>

EXTEND_TOP baldur25.bcs ".../ascension-inline/dream_state.baf"

// test the epilogues

<<<<<<<< .../ascension-inline/epilog_global.baf
IF 
   Global("DW_test_epilogues","GLOBAL",1)
THEN
    RESPONSE #100
       SetGlobal("DW_test_epilogues","GLOBAL",2)
       ActionOverride(Player1,LeaveAreaLUA("AR6200","",[945.1010],NW))
       ActionOverride(Player2,LeaveAreaLUA("AR6200","",[947.1013],NW))
       ActionOverride(Player3,LeaveAreaLUA("AR6200","",[849.1020],NW))
       ActionOverride(Player4,LeaveAreaLUA("AR6200","",[991.1009],NW))
       ActionOverride(Player5,LeaveAreaLUA("AR6200","",[997.994],NW))
       ActionOverride(Player6,LeaveAreaLUA("AR6200","",[996.1087],NW))
END
>>>>>>>>

<<<<<<<< .../ascension-inline/epilog_area.baf
IF 
   Global("DW_test_epilogues","GLOBAL",2)
THEN
    RESPONSE #100
       SetGlobal("DW_test_epilogues","GLOBAL",0)
       ActionOverride("finmel01",DestroySelf())
       SetGlobal("StartEndBios","AR6200",1)
END
>>>>>>>>

EXTEND_TOP "baldur25.bcs" ".../ascension-inline/epilog_global.baf"
EXTEND_TOP "ar6200.bcs" ".../ascension-inline/epilog_area.baf"

// test aspects of Ascension

<<<<<<<< .../ascension-inline/asc_pp.baf
IF
  Global("DW_test_ascension","GLOBAL",1)   // Balthazar dead
THEN
    RESPONSE #100
             SetGlobal("DW_test_ascension","GLOBAL",0)
             SetGlobal("LeavingPocketPlane","AR4500",1)
             SetGlobal("GavePocketPlane","GLOBAL",1)
             SetGlobal("ChallengesDone","GLOBAL",1)
             SetGlobal("SPRITE_IS_DEADBALTH","GLOBAL",1)
             SetGlobal("fin_balth_checkpoint_passed","GLOBAL",1)
END

IF
  Global("DW_test_ascension","GLOBAL",2)   // Balthazar allied
THEN
    RESPONSE #100
             SetGlobal("DW_test_ascension","GLOBAL",0)
             SetGlobal("LeavingPocketPlane","AR4500",1)
             SetGlobal("GavePocketPlane","GLOBAL",1)
             SetGlobal("ChallengesDone","GLOBAL",1)
             SetGlobal("BalthazarFights","GLOBAL",1)
             SetGlobal("SPRITE_IS_DEADBALTH","GLOBAL",0)
             SetGlobal("fin_balth_checkpoint_passed","GLOBAL",1)
END

IF
  Global("DW_test_ascension","GLOBAL",3) // Sarevok on side
THEN
    RESPONSE #100
             ChangeAlignment("sarevok",CHAOTIC_GOOD)
             SetGlobal("fin_sarev_redeemed","GLOBAL",1)
             SetGlobal("DW_test_ascension","GLOBAL",0)
END

IF
  Global("DW_test_ascension","GLOBAL",4) // skip to Final Five
THEN
    RESPONSE #100
            SetGlobal("DW_test_asc_setup_in_progress","GLOBAL",1)
            SetGlobal("DW_test_ascension","GLOBAL",0)
END

IF
  Global("DW_test_ascension","GLOBAL",5) // just enter PP with no Melissan
THEN
    RESPONSE #100
            SetGlobal("DW_test_asc_setup_in_progress","GLOBAL",2)
            SetGlobal("DW_test_ascension","GLOBAL",0)
END

IF
  Global("DW_test_ascension","GLOBAL",6) // skip to pools
THEN
    RESPONSE #100
            SetGlobal("DW_test_asc_setup_in_progress","GLOBAL",3)
            SetGlobal("DW_test_ascension","GLOBAL",0)
END

IF
  Global("DW_test_ascension","GLOBAL",7) // Bodhi recruitable
THEN
    RESPONSE #100
             ChangeAlignment(Player1,CHAOTIC_EVIL)
             ReputationInc(-10)
             ApplySpell(Player1,WIZARD_FRIENDS)
             SetGlobal("DW_test_ascension","GLOBAL",0)
END

IF
  Global("DW_test_ascension","GLOBAL",8) // Final conversation
THEN
    RESPONSE #100
       SetGlobal("fin_test_disable_melissan_initial","GLOBAL",1)
            SetGlobal("DW_test_asc_setup_in_progress","GLOBAL",4)
            SetGlobal("allfive","GLOBAL",1)
             SetGlobal("DW_test_ascension","GLOBAL",0)
END
>>>>>>>>

EXTEND_TOP ar4500.bcs ".../ascension-inline/asc_pp.baf"



ACTION_IF FILE_EXISTS_IN_GAME "fingrom.cre" BEGIN // i.e. we've already installed main component

   <<<<<<<< .../ascension-inline/mel_test_top.baf
IF
  Global("DW_test_remove_melissan","GLOBAL",1)
THEN
    RESPONSE #100
             SetGlobal("DW_test_remove_melissan","GLOBAL",0)
             DestroySelf()
END

IF
  GlobalGT("DW_test_asc_setup_in_progress","GLOBAL",0)
THEN
    RESPONSE #100
       NoAction()
END
>>>>>>>>

EXTEND_TOP finmel01.bcs ".../ascension-inline/mel_test_top.baf"


<<<<<<<< .../ascension-inline/ar6200_test_top.baf
IF
  Global("DW_test_asc_setup_in_progress","GLOBAL",1)
THEN
    RESPONSE #100
             SetGlobal("FinalFight","AR6200",6)
             SetGlobal("DW_test_asc_setup_in_progress","GLOBAL",0)
END

IF
  Global("DW_test_asc_setup_in_progress","GLOBAL",2)
THEN
    RESPONSE #100
             SetGlobal("DW_test_asc_setup_in_progress","GLOBAL",0)
             SetGlobal("DW_test_remove_melissan","GLOBAL",1)
END

IF
  Global("DW_test_asc_setup_in_progress","GLOBAL",3)
THEN
    RESPONSE #100
             SetGlobal("DW_test_asc_setup_in_progress","GLOBAL",0)
             SetGlobal("FinalFight","AR6200",3)
             SetGlobal("DW_test_remove_melisan","GLOBAL",1)
             AmbientActivate("AMB_Stream1",TRUE)
             AmbientActivate("AMB_Stream2",TRUE)
             AmbientActivate("AMB_Stream3",TRUE)
             AmbientActivate("MMELSPIN",TRUE)
             AmbientActivate("AM6200A",TRUE)
             AmbientActivate("AM6200B",FALSE)
END

IF
  Global("DW_test_asc_setup_in_progress","GLOBAL",4)
THEN
    RESPONSE #100
             SetGlobal("DW_test_asc_setup_in_progress","GLOBAL",0)
             SetGlobal("FinalFight","AR6200",8)
END
>>>>>>>>

EXTEND_TOP ar6200.bcs ".../ascension-inline/ar6200_test_top.baf"


END




// Balthazar confrontation

<<<<<<<< .../ascension-inline/balth_global.baf
IF 
   Global("DW_test_balth","GLOBAL",1)
THEN
    RESPONSE #100
       SetGlobal("DW_test_balth","GLOBAL",2)
       SetGlobal("metbal","GLOBAL",1)
       SetGlobal("SPRITE_IS_DEADBALTH","GLOBAL",0)
       ActionOverride(Player1,LeaveAreaLUA("AR5505","",[945.1010],NW))
       ActionOverride(Player2,LeaveAreaLUA("AR5505","",[947.1013],NW))
       ActionOverride(Player3,LeaveAreaLUA("AR5505","",[849.1020],NW))
       ActionOverride(Player4,LeaveAreaLUA("AR5505","",[991.1009],NW))
       ActionOverride(Player5,LeaveAreaLUA("AR5505","",[997.994],NW))
       ActionOverride(Player6,LeaveAreaLUA("AR5505","",[996.1087],NW))
END
>>>>>>>>

EXTEND_TOP baldur25.bcs ".../ascension-inline/balth_global.baf"

<<<<<<<< .../ascension-inline/balth_area.baf
IF
	Global("DW_test_balth","GLOBAL",2)
	Global("BalthazarEncounter","AR5505",0)
THEN
	RESPONSE #100
		ClearAllActions()
		SetGlobal("BalthazarEncounter","AR5505",1)
		StartCutSceneMode()
		StartCutScene("cut242a")
END
>>>>>>>>

EXTEND_TOP ar5505.bcs ".../ascension-inline/balth_area.baf"

// Gromnir showdown

<<<<<<<< .../ascension-inline/gromnir_global.baf
IF
  Global("DW_test_gromnir","GLOBAL",1)
THEN
    RESPONSE #100   
             SetGlobal("EnteredAR5001","GLOBAL",0)
             SetGlobal("DW_test_gromnir","GLOBAL",0)
             SetGlobal("TalkedSarMel01","GLOBAL",1)
             SetGlobal("MelissanJob","GLOBAL",1)
             SetGlobal("SPRITE_IS_DEADGROMNIR","GLOBAL",0)
       ActionOverride(Player1,LeaveAreaLUA("AR5001","",[800.600],SSW))
       ActionOverride(Player2,LeaveAreaLUA("AR5001","",[810.600],SSW))
       ActionOverride(Player3,LeaveAreaLUA("AR5001","",[800.610],SSW))
       ActionOverride(Player4,LeaveAreaLUA("AR5001","",[800.590],SSW))
       ActionOverride(Player5,LeaveAreaLUA("AR5001","",[790.600],SSW))
       ActionOverride(Player6,LeaveAreaLUA("AR5001","",[790.590],SSW))
END

IF
  Global("DW_test_gromnir","GLOBAL",2)
THEN
    RESPONSE #100
             SetGlobal("DW_test_gromnir","GLOBAL",0)
             SetGlobal("TalkedSarMel01","GLOBAL",1)
             SetGlobal("MelissanJob","GLOBAL",1)
END
>>>>>>>>


EXTEND_TOP baldur25.bcs ".../ascension-inline/gromnir_global.baf"

// Yaga-Shura showdown

<<<<<<<< .../ascension-inline/yaga_global.baf
IF
  Global("DW_test_yaga","GLOBAL",1)
THEN
    RESPONSE #100
             SetGlobal("DW_test_yaga","GLOBAL",0)
             SetGlobal("YagaShuraHeart","GLOBAL",1)
             SetGlobal("TalkedSarMel01","GLOBAL",1)
             SetGlobal("MelissanJob","GLOBAL",1)
             SetGlobal("SPRITE_IS_DEADGROMNIR","GLOBAL",1)
             SetGlobal("Mel01GromnirDeath","GLOBAL",1)
             SetGlobal("DeathOfGromnir","GLOBAL",1)
       ActionOverride(Player1,LeaveAreaLUA("AR5203","",[3400.700],SSW))
       ActionOverride(Player2,LeaveAreaLUA("AR5203","",[3400.710],SSW))
       ActionOverride(Player3,LeaveAreaLUA("AR5203","",[3400.690],SSW))
       ActionOverride(Player4,LeaveAreaLUA("AR5203","",[3410.700],SSW))
       ActionOverride(Player5,LeaveAreaLUA("AR5203","",[3390.700],SSW))
       ActionOverride(Player6,LeaveAreaLUA("AR5203","",[3410.710],SSW))
END
>>>>>>>>

EXTEND_TOP baldur25.bcs ".../ascension-inline/yaga_global.baf"

// Abazigal battle

<<<<<<<< .../ascension-inline/abaz_global.baf
IF
  Global("DW_test_abaz","GLOBAL",1)
THEN
    RESPONSE #100
             SetGlobal("DW_test_abaz","GLOBAL",0)
             SetGlobal("SPRITE_IS_DEADABAZIGAL","GLOBAL",0)
       ActionOverride(Player1,LeaveAreaLUA("AR6005","",[2300.700],SSW))
       ActionOverride(Player2,LeaveAreaLUA("AR6005","",[2310.700],SSW))
       ActionOverride(Player3,LeaveAreaLUA("AR6005","",[2290.700],SSW))
       ActionOverride(Player4,LeaveAreaLUA("AR6005","",[2300.710],SSW))
       ActionOverride(Player5,LeaveAreaLUA("AR6005","",[2300.690],SSW))
       ActionOverride(Player6,LeaveAreaLUA("AR6005","",[2310.690],SSW))
END
>>>>>>>>

EXTEND_TOP baldur25.bcs ".../ascension-inline/abaz_global.baf"

// Illasera battle

<<<<<<<< .../ascension-inline/illa_global.baf
IF
  Global("DW_test_illa","GLOBAL",1)
THEN
    RESPONSE #100
        SetGlobal("DW_test_illa","GLOBAL",2)
	SetGlobal("DreamSolarTimer","GLOBAL",999999)
	SetGlobal("SolarDream","GLOBAL",0)
	SetGlobal("SPRITE_IS_DEADILLASERA","GLOBAL",0)
        ActionOverride(Player1,LeaveAreaLUA("AR4000","",[680.460],SSW))
        ActionOverride(Player2,LeaveAreaLUA("AR4000","",[700.460],SSW))
        ActionOverride(Player3,LeaveAreaLUA("AR4000","",[720.460],SSW))
        ActionOverride(Player4,LeaveAreaLUA("AR4000","",[700.480],SSW))
        ActionOverride(Player5,LeaveAreaLUA("AR4000","",[700.440],SSW))
        ActionOverride(Player6,LeaveAreaLUA("AR4000","",[720.480],SSW))
END
>>>>>>>>

EXTEND_TOP baldur25.bcs ".../ascension-inline/illa_global.baf"

<<<<<<<< .../ascension-inline/illa_ar4000.baf
IF
  Global("DW_test_illa","GLOBAL",2)
THEN
    RESPONSE #100
        SetGlobal("DW_test_illa","GLOBAL",0)
	SetGlobal("IllaseraSpawnTimer","GLOBAL",0)
	SetGlobal("IllaseraSpawn","GLOBAL",0)
	SetGlobal("SetSolarDreamTimer","AR4000",0)
        ActionOverride(Player1,LeaveAreaLUA("AR4000","",[680.460],SSW))
        ActionOverride(Player2,LeaveAreaLUA("AR4000","",[700.460],SSW))
        ActionOverride(Player3,LeaveAreaLUA("AR4000","",[720.460],SSW))
        ActionOverride(Player4,LeaveAreaLUA("AR4000","",[700.480],SSW))
        ActionOverride(Player5,LeaveAreaLUA("AR4000","",[700.440],SSW))
        ActionOverride(Player6,LeaveAreaLUA("AR4000","",[720.480],SSW))
END
>>>>>>>>

EXTEND_TOP ar4000.bcs ".../ascension-inline/illa_ar4000.baf"

// testing spells

COPY_EXISTING "%WIZARD_BREACH%.spl" "override/dw#test2.spl"
    SAY NAME1 "AoE Breach (test)"
    LPF ALTER_SPELL_HEADER INT_VAR projectile=254 END

COPY_EXISTING "%WIZARD_SPELL_STRIKE%.spl" "override/dw#test3.spl"
    SAY NAME1 "AoE Spellstrike (test)"
    LPF ALTER_SPELL_HEADER INT_VAR projectile=254 END

COPY_EXISTING "dw#test2.spl" "override/dw#test1.spl"
    SAY NAME1 "AoE megadamage (test)"
    LPF DELETE_EFFECT END
    LPF ADD_SPELL_EFFECT INT_VAR opcode=12 target=2 timing=1 parameter1=1000 parameter2=0 END
    LPF CLONE_EFFECT INT_VAR match_parameter2=0 parameter2=0x10000 END
    LPF CLONE_EFFECT INT_VAR match_parameter2=0 parameter2=0x20000 END

<<<<<<<< .../ascension-inline/dw_zap.baf
IF
  HotKey(E)
THEN
    RESPONSE #100
        ReallyForceSpellRES("dw#test1",Myself)
END

IF
  HotKey(F)
THEN
    RESPONSE #100
        ReallyForceSpellRES("dw#test2",Myself)
END

IF
  HotKey(G)
THEN
    RESPONSE #100
        ReallyForceSpellRES("dw#test3",Myself)
END

>>>>>>>>

COPY ".../ascension-inline/dw_zap.baf" "scripts/dw_zap.bs" COMPILE_BAF_TO_BCS

END