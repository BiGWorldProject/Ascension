///////////////////////////////////////////////////////////////
/// Fragments of DavidW's SFO function library
///////////////////////////////////////////////////////////////

////// Blank file

<<<<<<<< .../stratagems-inline/blank
>>>>>>>>

///// Initial definitions

OUTER_SPRINT external_loc weidu_external
MKDIR "%external_loc%"
OUTER_SPRINT workspace "%external_loc%/workspace"
MKDIR "%workspace%"
OUTER_SPRINT marker_loc "%external_loc%/markers"
MKDIR "%marker_loc%"

OUTER_SPRINT scsroot ascension
OUTER_SPRINT MOD_FOLDER ascension  // to avoid confusing anyone in the future
OUTER_SPRINT MOD_VERSION"1.7.2-dw" // in due course WEIDU will do this automatically

ACTION_IF GAME_IS "bg2ee eet" BEGIN
   OUTER_SET enhanced_edition=1
END ELSE BEGIN
   OUTER_SET enhanced_edition=0
END

OUTER_SPRINT ext_lang_loc "%external_loc%/lang/%scsroot%"

////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////    immutable version of HANDLE_CHARSETS
////////////
////////////    Assumes "scsroot" has been set to your mod directory, and "workspace" to your working
////////////    directory
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION handle_charsets_immutably
       INT_VAR force_tra_rebuild=0
       STR_VAR tra_base=lang
               base_language=english
               iconv_path=""
       RET     scs_tra_loc
BEGIN
   // guess scsroot and workspace if not set
   // do conversions (ee only)
   ACTION_IF enhanced_edition BEGIN
       OUTER_INNER_PATCH_SAVE versionstring "%MOD_VERSION%" BEGIN
          REPLACE_TEXTUALLY " " ""
       END
       OUTER_SPRINT scs_tra_loc "%ext_lang_loc%"
       OUTER_SPRINT marker_name "%marker_loc%/dw#%scsroot%_%versionstring%_languages_installed.mrk"
       ACTION_IF (!FILE_EXISTS "%marker_name%"  || force_tra_rebuild) BEGIN
          COPY ".../stratagems-inline/blank" "%marker_name%"
          MKDIR "%scs_tra_loc%/%LANGUAGE%"
          ACTION_BASH_FOR "%scsroot%/%tra_base%/%LANGUAGE%" ".*\.tra" BEGIN
             COPY "%BASH_FOR_FILESPEC%" "%scs_tra_loc%/%LANGUAGE%"
          END
          ACTION_MATCH "%LANGUAGE%" WITH
          "%base_language%" BEGIN END
          DEFAULT
            MKDIR "%scs_tra_loc%/%base_language%"
            ACTION_BASH_FOR "%scsroot%/%tra_base%/%base_language%" ".*\.tra" BEGIN
                COPY "%BASH_FOR_FILESPEC%" "%scs_tra_loc%/%base_language%"
            END
          END
          // guess iconv path if not given
          ACTION_IF "%iconv_path%" STRING_EQUAL_CASE "" BEGIN
             OUTER_SPRINT iconv_path "%scsroot%/%tra_base%/iconv"
          END
          LAF HANDLE_CHARSETS INT_VAR infer_charsets = 1 STR_VAR iconv_path  tra_path = ~%scs_tra_loc%~  END
       END
   END ELSE BEGIN
      // on non-EE, just set the tra loc
      OUTER_SPRINT scs_tra_loc "%scsroot%/%tra_base%"
   END
END