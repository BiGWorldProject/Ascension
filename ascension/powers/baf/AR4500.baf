/////////////////////////////////////////////////////////////////////////////////
//// Bhaalspawn powers all controlled from AR4500, triggering after you emerge
//// from the challenges.
//// 'Bhaal25Dreamx' tracks your good/evil choices in each conversation with the Solar
/////////////////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////////////////
/// After defeating first challenge (horde of your victims)
/////////////////////////////////////////////////////////////////////////////////

// good outcome: Mass Healing

IF
  GlobalGT("Bhaal25Dream1","GLOBAL",0)
  Dead("chalpc01")
  Dead("chtaz01")
  Dead("chang01")
  Dead("chsam01")
  Global("AscBhaal1","AR4500",0)
THEN
  RESPONSE #100
    SetGlobal("AscBhaal1","AR4500",1)
    CreateVisualEffectObject("SPSTRENH",Player1)
    AddJournalEntry(@2000,INFO)
    ActionOverride(Player1,AddSpecialAbility("BHAAL1A"))
END

// evil outcome: regeneration

IF
  !GlobalGT("Bhaal25Dream1","GLOBAL",0)
  Dead("chalpc01")
  Dead("chtaz01")
  Dead("chang01")
  Dead("chsam01")
  Global("AscBhaal1","AR4500",0)
THEN
  RESPONSE #100
    SetGlobal("AscBhaal1","AR4500",1)
    CreateVisualEffectObject("SPSTRENH",Player1)
    AddJournalEntry(@2001,INFO)
    ActionOverride(Player1,AddSpecialAbility("BHAAL1B"))
END

/////////////////////////////////////////////////////////////////////////////////
/// After defeating second challenge (Slayer)
/////////////////////////////////////////////////////////////////////////////////

// good outcome: Draw Upon Holy Might

IF
  Dead("chalslay")
  Global("AscBhaal2","AR4500",0)
  GlobalGT("Bhaal25Dream2","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("AscBhaal2","AR4500",1)
    CreateVisualEffectObject("SPSTRENH",Player1)
    AddJournalEntry(@2002,INFO)
    ActionOverride(Player1,AddSpecialAbility("BHAAL2A"))
END

// evil outcome: Dark Taint (hardcore poison ability)

IF
  Dead("chalslay")
  Global("AscBhaal2","AR4500",0)
  !GlobalGT("Bhaal25Dream2","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("AscBhaal2","AR4500",1)
    CreateVisualEffectObject("SPSTRENH",Player1)
    AddJournalEntry(@2003,INFO)
    ActionOverride(Player1,AddSpecialAbility("BHAAL2B"))
END

/////////////////////////////////////////////////////////////////////////////////
/// After defeating third challenge (Cyric's mooks)
/////////////////////////////////////////////////////////////////////////////////

// good outcome: Target=Self immunity to Energy Drain

IF
  Global("AscBhaal3","AR4500",0)
  GlobalGT("Bhaal25Dream3","GLOBAL",0)
  NumDead("chalcy02",3)
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("AscBhaal3","AR4500",1)
    CreateVisualEffectObject("SPSTRENH",Player1)
    AddJournalEntry(@2004,INFO)
    DisplayStringHead(Player1,@1039)
    ActionOverride(Player1,ApplySpellRES("BHAAL3A",Myself))
END

// evil outcome: Hand of Murder

IF
  Global("AscBhaal3","AR4500",0)
  !GlobalGT("Bhaal25Dream3","GLOBAL",0)
  NumDeadGT("chalcy02",2)
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("AscBhaal3","AR4500",1)
    CreateVisualEffectObject("SPSTRENH",Player1)
    AddJournalEntry(@2005,INFO)
    ActionOverride(Player1,AddSpecialAbility("BHAAL3B"))
END

/////////////////////////////////////////////////////////////////////////////////
/// After defeating final challenge (the Ravager)
/////////////////////////////////////////////////////////////////////////////////

// good outcome: Resurrection

IF
  GlobalGT("Bhaal25Dream5","GLOBAL",0)
  Dead("chalrv01")
  Global("AscBhaal4","AR4500",0)
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("AscBhaal4","AR4500",1)
    CreateVisualEffectObject("SPSTRENH",Player1)
    AddJournalEntry(@2006,INFO)
    ActionOverride(Player1,AddSpecialAbility("BHAAL4A"))
END

// evil outcome: Target=Self, 25% resistance to all forms of physical damage

IF
  !GlobalGT("Bhaal25Dream5","GLOBAL",0)
  Dead("chalrv01")
  Global("AscBhaal4","AR4500",0)
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("AscBhaal4","AR4500",1)
    CreateVisualEffectObject("SPSTRENH",Player1)
    AddJournalEntry(@2007,INFO)
    DisplayStringHead(Player1,@1040)
    ActionOverride(Player1,ApplySpellRES("BHAAL4B",Myself))
END

